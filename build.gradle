plugins {
	id 'java'
	id "com.github.johnrengelman.shadow" version "6.1.0"
}

repositories {
	mavenLocal() {
		content {
			includeGroup "net.runelite"
		}
	}
	maven {
		url = 'https://repo.runelite.net'
	}
	mavenCentral()
}

def runeLiteVersion = '1.7.16'

dependencies {
	compileOnly group: 'net.runelite', name:'client', version: runeLiteVersion

	compileOnly 'org.projectlombok:lombok:1.18.4'
	annotationProcessor 'org.projectlombok:lombok:1.18.4'

	testCompileOnly 'org.projectlombok:lombok:1.18.4'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.4'

	testImplementation 'junit:junit:4.12'
	testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
	testImplementation group: 'net.runelite', name:'jshell', version: runeLiteVersion
}

group = 'abex.os.debug'
version = file("version").text.trim()
sourceCompatibility = '1.8'

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

tasks.withType(AbstractArchiveTask) {
	preserveFileTimestamps = false
	reproducibleFileOrder = true
}

shadowJar {
	from sourceSets.test.output
	configurations = [project.configurations.testRuntimeClasspath]
	manifest {
		attributes "Main-Class": "abex.os.debug.DebugPluginTest"
	}
	archiveFileName = "runelite-${runeLiteVersion}+debugplugin-${version}.jar"
	destinationDirectory.set(file("${buildDir}/shadowjar/"))
}