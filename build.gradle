plugins {
	id 'java'
}

repositories {
	mavenLocal() {
		content {
			includeGroup "net.runelite"
		}
	}
	maven {
		url = 'https://repo.runelite.net'
	}
	mavenCentral()
}

def runeLiteVersion = '1.9.8'

dependencies {
	compileOnly group: 'net.runelite', name:'client', version: runeLiteVersion

	compileOnly 'org.projectlombok:lombok:1.18.4'
	annotationProcessor 'org.projectlombok:lombok:1.18.4'

	testCompileOnly 'org.projectlombok:lombok:1.18.4'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.4'

	testImplementation 'junit:junit:4.12'
	testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
	testImplementation group: 'net.runelite', name:'jshell', version: runeLiteVersion
}

group = 'abex.os.debug'
version = file("version").text.trim()
sourceCompatibility = '1.8'

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

tasks.withType(AbstractArchiveTask) {
	preserveFileTimestamps = false
	reproducibleFileOrder = true
}

task shadowJar(type: Jar) {
	archiveFileName = "runelite-${runeLiteVersion}+debugplugin.jar"
	from sourceSets.main.output, sourceSets.test.output
	from {
		project.configurations.testRuntimeClasspath.collect {
			it.isDirectory() ? it : project.zipTree(it)
		}
	}

	exclude "META-INF/INDEX.LIST"
	exclude "**/module-info.class"
	exclude "META-INF/*.RSA"
	exclude "META-INF/*.SF"
	manifest {
		attributes(
			"Main-Class": "abex.os.debug.DebugPluginTest"
		)
	}
}